ext.configureAndroidLibrary = { project ->
    project.apply plugin: 'com.android.library'
    configureAndroidModule(project)
}

ext.configureAndroidApp = { project ->
    project.apply plugin: 'com.android.application'
    configureAndroidModule(project)
}

private def configureAndroidModule(Project project) {
    project.android {
        compileSdkVersion deps.build.compileSdkVersion

        defaultConfig {
            minSdkVersion deps.build.minSdkVersion
            targetSdkVersion deps.build.targetSdkVersion
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testOptions.unitTests.includeAndroidResources = true
    }
}

ext.configureDetektForAllSubprojects = { Project project ->
    project.task('detektBaseline')
    project.subprojects {
        afterEvaluate { subProject ->
            if (subProject.plugins.hasPlugin("kotlin") || subProject.plugins.hasPlugin("kotlin-android")) {
                subProject.apply plugin: "io.gitlab.arturbosch.detekt"

                subProject.detekt {
                    parallel = true
                    input = files("${subProject.projectDir}/src/")
                    config = files("${project.rootDir}/detekt.yml")
                    filters = ".*test.*, .*/resources/.*"
                    def baselineFile = file("${subProject.projectDir}/detekt-baseline.xml")
                    if (baselineFile.exists()) {
                        baseline = baselineFile
                    }
                }

                project.tasks.detektBaseline.dependsOn(detektBaseline)
            }
        }
    }
}
