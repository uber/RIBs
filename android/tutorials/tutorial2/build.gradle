/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    dependencies {
        classpath deps.build.gradlePlugins.android
        classpath deps.build.gradlePlugins.errorprone
    }
}

apply plugin: 'net.ltgt.errorprone'
apply plugin: 'com.android.application'

android {
    compileSdkVersion deps.build.compileSdkVersion
    buildToolsVersion deps.build.buildToolsVersion

    defaultConfig {
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        applicationId "com.uber.tutorial2"
        versionCode 1
        versionName "1.0"
    }

    // No need for lint. This is just a tutorial.
    lintOptions {
        abortOnError false
        quiet true
    }
}

dependencies {
    annotationProcessor deps.apt.daggerCompiler
    annotationProcessor project(":libraries:rib-compiler-test")
    annotationProcessor deps.apt.nullAway
    compile project(":libraries:rib-android")
    compile 'com.android.support:percent:25.0.1'
    compile deps.external.dagger
    compile deps.external.rxbinding
    provided deps.apt.androidApi
    provided deps.apt.javax
    testCompile project(":libraries:rib-test-utils")
    errorprone deps.build.errorProne
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xep:CheckReturnValue:OFF"]
    // NullAway configuration
    if (!name.toLowerCase().contains("test")) {
        options.compilerArgs += [
                "-Xep:NullAway:ERROR",
                "-XepOpt:NullAway:AnnotatedPackages=com.uber",
                // Dagger doesn't pass the NullAway checks. So we
                // use the following flag to disable Error Prone on generated code.
                "-XepExcludedPaths:.*/build/generated/.*"]
    }
}
